apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

final modules_with_viewbinding = ['browser-preferred']
if (name in modules_with_viewbinding) {
    android {
        buildFeatures.viewBinding = true
    }
} else {
    apply plugin: 'kotlin-android-extensions'

    androidExtensions {
        experimental true
    }
}

android {
    compileSdkVersion versions.compileSdk

    buildFeatures {
        resValues = false
    }

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {
    if (!project.name.startsWith('base')) implementation project(':base')

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation 'androidx.core:core-ktx:1.5.0-beta01'
    implementation 'androidx.appcompat:appcompat:1.3.0-beta01'
    implementation 'androidx.fragment:fragment-ktx:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-beta01'
    implementation 'com.google.android.material:material:1.3.0'

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    implementation "com.google.dagger:dagger-android-support:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"
    kapt "com.google.dagger:dagger-android-processor:$versions.dagger"
}
