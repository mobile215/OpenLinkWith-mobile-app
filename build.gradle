buildscript {
    ext.ci = 'true' == System.getenv('CI')
    ext.isPlay = project.hasProperty('play')
    ext.configFile = { fileName ->
        rootProject.file("gradle/$fileName")
    }

    def versionMajor = 2
    def versionMinor = 7
    def versionBuild = 0
    ext.playPublishTrack = 'internal'

    ext.versions = [
            compileSdk : 28,
            minSdk     : 22,
            versionCode: versionMajor * 10000 + versionMinor * 100 + versionBuild,
            versionName: "${versionMajor}.${versionMinor}",

            dagger     : '2.27',
            lifecycle  : '2.2.0',
            kotlin     : '1.3.70',
            ktlint     : '0.36.0',
            okHttp     : '4.4.1',
            room       : '2.2.5',
    ]

    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0-beta03'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.28.0'
        classpath 'com.novoda:gradle-android-command-plugin:2.1.0'
        classpath 'com.novoda:gradle-build-properties-plugin:0.4.1'
        classpath 'io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.7.0'
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:9.2.1'
        classpath 'com.novoda:gradle-static-analysis-plugin:1.2'
        if (isPlay) {
            classpath 'com.google.firebase:firebase-crashlytics-gradle:2.0.0-beta02'
            classpath 'com.github.triplet.gradle:play-publisher:2.7.2'
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    if (project.name != 'test-support') {
        apply from: configFile('static-analysis.gradle')
    }
}

apply from: configFile('configure-not-wanted-repositories.gradle')
apply from: configFile('configure-dependency-resolution.gradle')
apply from: configFile('configure-dependency-updates.gradle')
