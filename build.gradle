buildscript {
    ext.ci = 'true' == System.getenv('CI')
    ext.isPlay = project.hasProperty('play')
    ext.configFile = { fileName ->
        rootProject.file("gradle/$fileName")
    }

    def versionMajor = 2
    def versionMinor = 9
    def versionBuild = 1
    ext.playPublishTrack = 'internal'

    ext.versions = [
            compileSdk : 29,
            minSdk     : 22,
            versionCode: versionMajor * 10000 + versionMinor * 100 + versionBuild,
            versionName: "${versionMajor}.${versionMinor}",

            dagger     : '2.32',
            lifecycle  : '2.3.0',
            kotlin     : '1.4.30',
            ktlint     : '0.39.0',
            okHttp     : '4.8.0',
            room       : '2.3.0-beta01',
    ]

    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.36.0'
        classpath 'com.novoda:gradle-build-properties-plugin:0.4.1'
        classpath 'io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.14.1'
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:10.0.0'
        classpath 'com.gradleup:static-analysis-plugin:1.3'
        classpath 'com.gradleup:auto-manifest-plugin:1.0.4'
        if (isPlay) {
            classpath 'com.google.gms:google-services:4.3.5'
            classpath 'com.google.firebase:firebase-crashlytics-gradle:2.5.0'
            classpath 'com.github.triplet.gradle:play-publisher:3.0.0'
        }
    }
}
apply plugin: 'com.gradleup.auto.manifest'

autoManifest {
    packageName = 'com.tasomaniac.openwith'
    replaceDashesWithDot = true
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    if (project.name != 'test-support' && project.name != 'base-translations') {
        apply from: configFile('static-analysis.gradle')
    }
}

apply from: configFile('configure-not-wanted-repositories.gradle')
apply from: configFile('configure-dependency-updates.gradle')
