buildscript {
    ext.ci = 'true' == System.getenv('CI')
    ext.isPlay = project.hasProperty('play')
    ext.configFile = { fileName ->
        rootProject.file("gradle/$fileName")
    }

    def versionMajor = 2
    def versionMinor = 6
    def versionBuild = 1
    ext.playPublishTrack = 'internal'

    ext.versions = [
            compileSdk : 28,
            minSdk     : 22,
            versionCode: versionMajor * 10000 + versionMinor * 100 + versionBuild,
            versionName: "${versionMajor}.${versionMinor}",

            dagger     : '2.22.1',
            lifecycle  : '2.0.0',
            kotlin     : '1.3.21',
            ktlint     : '0.31.0',
            okHttp     : '3.14.0',
            room       : '2.0.0',
    ]

    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        jcenter {
            content {
                includeGroup 'org.jetbrains.trove4j'
            }
        }
        if (isPlay) {
            maven { url 'https://maven.fabric.io/public' }
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0-alpha09'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
        classpath 'com.github.triplet.gradle:play-publisher:2.1.1'
        classpath 'com.novoda:gradle-android-command-plugin:2.0.1'
        classpath 'com.novoda:gradle-build-properties-plugin:0.4.1'
        classpath 'io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.0.0-RC14'
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:7.2.1'
        classpath 'com.novoda:gradle-static-analysis-plugin:0.8.1'
        if (isPlay) {
            classpath 'io.fabric.tools:gradle:1.28.1'
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

ext.ReporterType = org.jlleitschuh.gradle.ktlint.reporter.ReporterType
apply plugin: 'com.github.ben-manes.versions'
apply from: configFile('configure-not-wanted-repositories.gradle')
apply from: configFile('configure-dependency-resolution.gradle')
apply from: configFile('configure-dependency-updates.gradle')
