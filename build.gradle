buildscript {
    ext.ci = 'true' == System.getenv('CI')
    ext.isPlay = project.hasProperty('play')

    def versionMajor = 2
    def versionMinor = 2
    def versionBuild = 0

    ext.versions = [
            compileSdk : 28,
            minSdk     : 19,
            versionCode: versionMajor * 10000 + versionMinor * 100 + versionBuild,
            versionName: "${versionMajor}.${versionMinor}",

            dagger     : '2.16',
            lifecycle  : '2.0.0-rc01',
            kotlin     : '1.2.61',
            okHttp     : '3.11.0',
            room       : '2.0.0-rc01',
    ]

    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://novoda.bintray.com/snapshots' }
        if (isPlay) {
            maven { url 'https://maven.fabric.io/public' }
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-alpha06'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath 'com.github.triplet.gradle:play-publisher:1.2.2'
        classpath 'com.novoda:gradle-android-command-plugin:2.0.1'
        classpath 'com.novoda:gradle-build-properties-plugin:0.4.1'
        classpath 'com.novoda:gradle-static-analysis-plugin:0.6'
        if (isPlay) {
            classpath 'io.fabric.tools:gradle:1.25.4'
        }
    }
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    configurations.all {
        resolutionStrategy.eachDependency {
            if (requested.group == 'org.jetbrains.kotlin') {
                useVersion versions.kotlin
            }
        }
    }
}
