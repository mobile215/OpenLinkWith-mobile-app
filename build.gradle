buildscript {
    ext.ci = 'true' == System.getenv('CI')
    ext.isPlay = project.hasProperty('play')
    ext.configFile = { fileName ->
        rootProject.file("gradle/$fileName")
    }

    def versionMajor = 2
    def versionMinor = 7
    def versionBuild = 0
    ext.playPublishTrack = 'internal'

    ext.versions = [
            compileSdk : 28,
            minSdk     : 22,
            versionCode: versionMajor * 10000 + versionMinor * 100 + versionBuild,
            versionName: "${versionMajor}.${versionMinor}",

            dagger     : '2.25.2',
            lifecycle  : '2.2.0-rc01',
            kotlin     : '1.3.50',
            ktlint     : '0.35.0',
            okHttp     : '4.2.2',
            room       : '2.2.1',
    ]

    repositories {
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        gradlePluginPortal()
        google()
        mavenCentral()
        jcenter {
            content { includeGroup 'org.jetbrains.trove4j' }
        }
        if (isPlay) {
            maven { url 'https://maven.fabric.io/public' }
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0-alpha01'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.27.0'
        classpath 'com.novoda:gradle-android-command-plugin:2.1.0'
        classpath 'com.novoda:gradle-build-properties-plugin:0.4.1'
        classpath 'io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.1.1'
        classpath 'org.jlleitschuh.gradle:ktlint-gradle:9.0.0'
        classpath 'com.novoda:gradle-static-analysis-plugin:1.1'
        if (isPlay) {
            classpath 'com.github.triplet.gradle:play-publisher:2.4.2'
            classpath 'io.fabric.tools:gradle:1.31.2'
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    if (project.name != 'test-support') {
        apply from: configFile('static-analysis.gradle')
    }
}

apply plugin: 'com.github.ben-manes.versions'
apply from: configFile('configure-not-wanted-repositories.gradle')
apply from: configFile('configure-dependency-resolution.gradle')
apply from: configFile('configure-dependency-updates.gradle')
