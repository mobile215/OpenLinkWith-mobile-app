buildscript {
    ext.ci = 'true' == System.getenv('CI')
    ext.isPlay = project.hasProperty('play')

    def versionMajor = 2
    def versionMinor = 4
    def versionBuild = 0

    ext.versions = [
            compileSdk : 28,
            minSdk     : 19,
            versionCode: versionMajor * 10000 + versionMinor * 100 + versionBuild,
            versionName: "${versionMajor}.${versionMinor}",

            dagger     : '2.19',
            lifecycle  : '2.0.0',
            kotlin     : '1.3.0',
            okHttp     : '3.11.0',
            room       : '2.0.0',
    ]

    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://novoda.bintray.com/snapshots' }
        if (isPlay) {
            maven { url 'https://maven.fabric.io/public' }
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-beta04'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath 'com.github.triplet.gradle:play-publisher:1.2.2'
        classpath 'com.novoda:gradle-android-command-plugin:2.0.1'
        classpath 'com.novoda:gradle-build-properties-plugin:0.4.1'
        classpath 'gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:5.1.0'
        classpath 'com.novoda:gradle-static-analysis-plugin:0.7'
        if (isPlay) {
            classpath 'io.fabric.tools:gradle:1.26.1'
        }
    }
}

ext.ReporterType = org.jlleitschuh.gradle.ktlint.reporter.ReporterType
apply plugin: 'com.github.ben-manes.versions'

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    configurations.all {
        resolutionStrategy.eachDependency {
            if (requested.group == 'org.jetbrains.kotlin') {
                useVersion versions.kotlin
            }
        }
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            if (selection.candidate.version ==~ /(?i).*[.-]alpha[.\d-]*/) {
                selection.reject('Release candidate')
            }
        }
    }
}
