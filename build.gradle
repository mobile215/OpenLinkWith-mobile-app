buildscript {
    ext.ci = 'true' == System.getenv('CI')
    ext.isPlay = project.hasProperty('play')

    def versionMajor = 2
    def versionMinor = 0
    def versionBuild = 0

    ext.versions = [
            compileSdk : 28,
            minSdk     : 19,
            versionCode: versionMajor * 10000 + versionMinor * 100 + versionBuild,
            versionName: "${versionMajor}.${versionMinor}",

            dagger     : '2.16',
            lifecycle  : '2.0.0-alpha1',
            kotlin     : '1.2.41',
            okHttp     : '3.10.0',
            room       : '2.0.0-alpha1',
    ]

    repositories {
        gradlePluginPortal()
        google()
        jcenter()
        if (isPlay) {
            maven { url 'https://maven.fabric.io/public' }
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-alpha18'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
        classpath 'com.github.triplet.gradle:play-publisher:1.2.2'
        classpath 'com.novoda:gradle-android-command-plugin:2.0.1'
        classpath 'com.novoda:gradle-build-properties-plugin:0.4.1'
        classpath 'com.novoda:gradle-static-analysis-plugin:0.5.2'
        if (isPlay) {
            classpath 'io.fabric.tools:gradle:1.25.3'
        }
    }
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
    repositories {
        google()
        jcenter()
    }

    configurations.all {
        resolutionStrategy.eachDependency {
            if (requested.group == 'org.jetbrains.kotlin') {
                useVersion versions.kotlin
            }
        }
    }
}
